include(ExternalProject)
include(GNUInstallDirs)

define_property(GLOBAL PROPERTY CATCH2_BUILT
  BRIEF_DOCS "Whether catch2 has been built"
  FULL_DOCS "Used to ensure catch2 only gets built once.")

get_property(IS_CATCH2_BUILT GLOBAL PROPERTY CATCH2_BUILT SET)

if (NOT IS_CATCH2_BUILT)
  set(CATCH2_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})

  set(CATCH2_INCLUDE_DIR ${CATCH2_INSTALL_PREFIX}/include)
  set(CATCH2_LIBRARY_DIR ${CATCH2_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR})
  set(CATCH2_LIBRARY ${CATCH2_LIBRARY_DIR}/libCatch2.a)
  set(CATCH2_WITH_MAIN_LIBRARY ${CATCH2_LIBRARY_DIR}/libCatch2Main.a)

  add_library(catch2 STATIC IMPORTED GLOBAL)
  set_target_properties(catch2 PROPERTIES IMPORTED_LOCATION ${CATCH2_LIBRARY})
  list(APPEND LPM_EXT_INCLUDE_DIRS ${CATCH2_INCLUDE_DIR})

  message(STATUS "Building Catch2 library ${CATCH2_LIBRARY}")

  set(CATCH2_CMAKE_OPTS
    -DCMAKE_INSTALL_PREFIX=${CATCH2_INSTALL_PREFIX}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
    -DBUILD_SHARED_LIBS=OFF
    -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
    -DCATCH_INSTALL_DOCS=OFF
    -DCATCH_INSTALL_EXTRAS=OFF
    -DCATCH_BUILD_TESTING=OFF
  )

  ExternalProject_Add(catch2_proj
    PREFIX ${CMAKE_CURRENT_BINARY_DIR}/ext/catch2
    SOURCE_DIR ${PROJECT_SOURCE_DIR}/ext/catch2
    BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/ext/catch2
    INSTALL_DIR ${CATCH2_INSTALL_PREFIX}
    CMAKE_ARGS ${CATCH2_CMAKE_OPTS}
    BUILD_COMMAND ${MAKE} -j 8
    LOG_CONFIGURE TRUE
    LOG_BUILD TRUE
    LOG_INSTALL TRUE
    )
  add_dependencies(catch2 catch2_proj)
endif()
